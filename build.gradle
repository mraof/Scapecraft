buildscript {
    repositories {
        jcenter()
        maven {
            name = "forge"
            url = "http://files.minecraftforge.net/maven"
        }
        maven {
            name = "sonatype"
            url = "https://oss.sonatype.org/content/repositories/snapshots/"
        }
    }
    gradle.projectsEvaluated {
        tasks.withType(JavaCompile) {
            options.compilerArgs << "-Xmaxerrs" << "10000"
        }
    }
    dependencies {
        classpath 'net.minecraftforge.gradle:ForgeGradle:2.2-SNAPSHOT'
    }
}

repositories {
    maven {
        name = "dynmap"
        url = "http://repo.mikeprimm.com/"
    }
    maven {
        name = "forge"
        url = "http://files.minecraftforge.net/maven"
    }
    maven {
        name = "sk89q"
        url = "http://maven.sk89q.com/repo/"
    }
    maven {
        name = "sponge"
        url = "http://repo.spongepowered.org/maven/"
    }
}

defaultTasks "runClient"

apply plugin: 'net.minecraftforge.gradle.forge'

version = System.getenv('BUILD_NUMBER') ?: "test"
group = "scapecraft"
archivesBaseName = "Scapecraft"

minecraft {
    version = "1.10.2-12.18.0.2008"
    runDir = "eclipse"
    mappings = "snapshot_20160518"

    replace "@VERSION@", project.version
    replaceIn "Scapecraft.java"
}

compileJava {
    dependencies {
        compile 'us.dynmap:DynmapCoreAPI:2.3-SNAPSHOT'
    }
}

sourceSets {
    main {
        java {
            srcDir 'src'
        }
        resources {
            srcDir 'resources'
        }
    }
}
processResources
{
    // replace stuff in mcmod.info, nothing else
    from(sourceSets.main.resources.srcDirs) {
        include 'mcmod.info'

        // replace version and mcversion
        expand 'version':project.version, 'mcversion':project.minecraft.version
    }

    // copy everything else, thats not the mcmod.info
    from(sourceSets.main.resources.srcDirs) {
        exclude 'mcmod.info'
    }
}
build {
    doLast{
        String filename = project.archivesBaseName + "-" + version + ".jar"
        copy{
            from 'build/libs/' + filename
            into 'build/release/'
        }
        file("build/release/" + filename).renameTo(file("build/release/Scapecraft.jar"))
    }
}

runClient {
    if(project.hasProperty('systemProp.socksProxyHost'))
    {
        systemProperty "socksProxyHost", System.getProperty('socksProxyHost')
        systemProperty "socksProxyPort", System.getProperty('socksProxyPort')
    }
    if(project.hasProperty('username')) {
        args "--username", project.username
        if(project.hasProperty('password')) {
            args "--password", project.password
        }
    }
}
