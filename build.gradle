buildscript {
    repositories {
        mavenCentral()
        maven {
            name = "forge"
            url = "http://files.minecraftforge.net/maven"
        }
        maven {
            name = "sonatype"
            url = "https://oss.sonatype.org/content/repositories/snapshots/"
        }
    }
    dependencies {
        classpath 'net.minecraftforge.gradle:ForgeGradle:1.2-SNAPSHOT'
    }
}

repositories {
    maven {
        name = "dynmap"
        url = "http://repo.mikeprimm.com/"
    }
}

defaultTasks "runClient"

apply plugin: 'forge'

version = System.getenv()['BUILD_NUMBER']
group = "scapecraft"
archivesBaseName = "Scapecraft"

minecraft {
    version = "1.7.10-10.13.4.1558-1.7.10"
    runDir = "eclipse"
    mappings = "stable_12"

    replace "@VERSION@", project.version
    replaceIn "Scapecraft.java"
}

dependencies {
    compile 'us.dynmap:DynmapCoreAPI:2.3-SNAPSHOT'
}

sourceSets {
    main {
        java {
            srcDir 'src'
        }
        resources {
            srcDir 'resources'
        }
    }
}
processResources
{
    // replace stuff in mcmod.info, nothing else
    from(sourceSets.main.resources.srcDirs) {
        include 'mcmod.info'

        // replace version and mcversion
        expand 'version':project.version, 'mcversion':project.minecraft.version
    }

    // copy everything else, thats not the mcmod.info
    from(sourceSets.main.resources.srcDirs) {
        exclude 'mcmod.info'
    }
}
build {
    doLast{
        String filename = project.archivesBaseName + ".jar"
        copy{
            from 'build/libs/' + filename
            into 'build/release/'
        }
        file("build/release/" + filename).renameTo(file("build/release/Scapecraft.jar"))
    }
}

runClient {
    if(project.hasProperty('systemProp.socksProxyHost'))
    {
        systemProperty "socksProxyHost", System.getProperty('socksProxyHost')
        systemProperty "socksProxyPort", System.getProperty('socksProxyPort')
    }
    if(project.hasProperty('username')) {
        args "--username", project.username
        if(project.hasProperty('password')) {
            args "--password", project.password
        }
    }
}

//Use lwjgl 2.9.3 instead of 2.9.1
extractUserDev{
    File devJson = new File(getOut().toString() + '/dev.json')
    if(devJson.exists())
    {
        String contents = new File(getOut().toString() + '/dev.json').getText('UTF-8')
        contents=contents.replaceAll('2.9.1', '2.9.3')
        devJson.write(contents, 'UTF-8')
    }
}
